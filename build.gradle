description = "Apereo uPortal $version"

// Build Scans provide detailed information about many aspects of your
// build right in your browser.  (https://scans.gradle.com/plugin)
apply plugin: 'com.gradle.build-scan'

// Adds support for Node.js scripts
apply plugin: 'com.moowork.node'
apply plugin: 'hsql-plugin'
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    mavenLocal()
}

dbSettings {
	database= "file:./uPortal/data/uPortal;hsqldb.tx=mvcc";
	dbName = "uPortal";
	port= "8887";

}

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenLocal()
    }
    dependencies {
        classpath 'com.gradle:build-scan-plugin:1.0'
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
        classpath 'com.uportal.hsql:uportal-hsql:5.0.0-SNAPSHOT'
        classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.6'
    }
}

node {
    // Version of node to use.
    version = '6.9.1'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

subprojects {
    apply plugin: 'findbugs'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.sherter.google-java-format'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    findbugs {
        toolVersion = '3.0.1'
        ignoreFailures = true
    }

    googleJavaFormat {
        toolVersion '1.3'
        options style: 'AOSP'
    }

    configurations {
        /*
         * Defines a 'provided' scope similar to the one in Maven.  Dependencies
         * defined using provided will not become transitive dependencies.
         */
        provided

        /*
         * Makes provided dependencies available on the compile classpath in a way
         * that works both from the command line and in Buildship.
         */
        compile.extendsFrom provided
    }

}
